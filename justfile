# Unkey Development Commands
# Run `just` to see all available commands

set dotenv-load := true
set positional-arguments := true

# Default: show help
default:
    @just --list

# ─────────────────────────────────────────────────────────────
# Setup & Installation
# ─────────────────────────────────────────────────────────────

# Install all dependencies
install: install-go
    pnpm --dir=web install --frozen-lockfile

# Install Go dependencies
install-go:
    go mod download

# Install required Homebrew tools (run `brew install just` first!)
install-brew-tools:
    @command -v tilt >/dev/null 2>&1 || { echo "Installing tilt..."; brew install tilt; }
    @command -v ctlptl >/dev/null 2>&1 || { echo "Installing ctlptl..."; brew install ctlptl; }
    @command -v minikube >/dev/null 2>&1 || { echo "Installing minikube..."; brew install minikube; }
    @command -v bazel >/dev/null 2>&1 || { echo "Installing bazel..."; brew install bazelisk; }

# ─────────────────────────────────────────────────────────────
# Development
# ─────────────────────────────────────────────────────────────

# Start dev environment (Tilt + minikube)
dev:
    @ctlptl apply -f ./dev/cluster.yaml
    @minikube addons enable metrics-server
    @tilt up -f ./dev/Tiltfile

# Stop tilt (keeps cluster)
down:
    @tilt down -f ./dev/Tiltfile

# Delete minikube cluster entirely
nuke:
    @minikube delete

# Start infrastructure services via docker-compose
up: pull
    @docker compose -f ./dev/docker-compose.yaml up -d planetscale mysql redis clickhouse s3 otel kafka restate ctrl --wait

# Stop and remove all services with volumes
clean:
    @docker compose -f ./dev/docker-compose.yaml down --volumes

# Pull latest Docker images
pull:
    @docker compose -f ./dev/docker-compose.yaml pull

# Run local dashboard
local-dashboard: install build
    pnpm --dir=web/apps/dashboard local

# ─────────────────────────────────────────────────────────────
# Unkey CLI
# ─────────────────────────────────────────────────────────────

# Run unkey CLI (e.g., just unkey dev seed local --slug 123)
unkey *args:
    bazel run --ui_event_filters=-info --noshow_progress //:unkey -- {{args}}

# ─────────────────────────────────────────────────────────────
# Build & Generate
# ─────────────────────────────────────────────────────────────

# Build all artifacts with Bazel
build:
    bazel build //...

# Build web services
build-web:
    pnpm --dir=web build

# Sync BUILD.bazel files
bazel:
    bazel mod tidy
    bazel run //:gazelle

# Generate code from protobuf and other sources
generate: generate-sql
    rm -rf ./gen || true
    rm ./pkg/db/*_generated.go || true
    go generate ./...
    bazel run //:gazelle
    go fmt ./...
    pnpm --dir=web fmt

# Generate SQL schema from Drizzle
generate-sql:
    @rm -f ./pkg/db/schema.sql || true
    @touch ./pkg/db/schema.sql
    @echo "-- Code generated by justfile. DO NOT EDIT." >> ./pkg/db/schema.sql
    @echo "--" >> ./pkg/db/schema.sql
    @echo "-- Source: web/internal/db/src/schema" >> ./pkg/db/schema.sql
    @rm -rf ./web/internal/db/out
    @cd web/internal/db && pnpm drizzle-kit generate --schema=src/schema/index.ts --dialect=mysql --out=out --name=init --breakpoints=false
    @echo "\n" >> ./web/internal/db/out/0000_init.sql
    @cat ./web/internal/db/out/0000_init.sql >> ./pkg/db/schema.sql
    @rm -rf ./web/internal/db/out

# ─────────────────────────────────────────────────────────────
# Testing & Quality
# ─────────────────────────────────────────────────────────────

# Run all tests
test:
    docker compose -f ./dev/docker-compose.yaml up -d mysql clickhouse s3 kafka restate ctrl --wait
    bazel test //... --test_output=errors
    @just clean-docker-test

# Clean up test containers
clean-docker-test:
    @docker rm -vf $(docker ps -q -f label="owner=dockertest") > /dev/null 2>&1 || true

# Format code and run linters
fmt:
    go fmt ./...
    go tool buf format -w
    go tool golangci-lint run
    pnpm --dir=web fmt

# Run fuzz tests
fuzz:
    #!/usr/bin/env bash
    set -euo pipefail
    files=$(grep -r --include='*_test.go' -l 'func Fuzz' .)
    for file in $files; do
        funcs=$(grep -oE 'func (Fuzz[a-zA-Z0-9_]*)' "$file" | sed 's/func //')
        for func in $funcs; do
            echo "Fuzzing $func in $file"
            parentDir=$(dirname "$file")
            go test "$parentDir" -run="$func" -fuzz="$func" -fuzztime=60s
        done
    done

# ─────────────────────────────────────────────────────────────
# Cleanup
# ─────────────────────────────────────────────────────────────

# Stop all containers and clean up Docker system
nuke-docker:
    docker stop $(docker ps -aq)
    docker system prune -af
    docker volume prune --all -f
